plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'io.github.jhspetersson'
version = '0.2.1'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jspecify:jspecify:1.0.0'
    testImplementation platform('org.junit:junit-bom:5.11.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from tasks.javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java

            pom {
                name = "${project.name}"
                description = 'Gatherers library for Java Stream API'
                url = 'https://github.com/jhspetersson/packrat'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'Ivan Petrichenko'
                        email = 'jhspetersson@gmail.com'
                        organizationUrl = 'https://github.com/jhspetersson'
                    }
                }

                scm {
                    connection = 'scm:git://github.com/jhspetersson/packrat.git'
                    developerConnection = 'scm:git:ssh://github.com/jhspetersson/packrat.git'
                    url = 'https://github.com/jhspetersson/packrat'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('fake-repo')
        }
    }
}

tasks.register('generatePom', GenerateMavenPom) {
    group = 'build'
    description = 'Generates the POM file without publishing.'
    destination = file("${layout.buildDirectory.asFile.get()}/libs/${rootProject.name}-${project.version}.pom")
    pom = publishing.publications.myPublication.pom

    doLast {
        println "POM file written to: ${destination}"
    }
}

build.finalizedBy {
    tasks.generatePom
}
